
IF 
   See([PC])
   AreaCheck("%bg1_eet_symbol%3300")
   NumTimesTalkedTo(0)
  THEN
    RESPONSE #100
	Dialogue([PC])	
END 	

// Talks 10
IF
PartyRested()
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
GlobalTimerExpired("X3milyOneHour","GLOBAL")
Global("X3milyBookTalk","GLOBAL",6)
THEN
RESPONSE #100
IncrementGlobal("X3milyBookTalk","GLOBAL",1)
StartDialogueNoSet(Player1)
END

IF 
Global("X3EmiRodwynIsaacDuel","GLOBAL",1)
Dead("X3Isaac")
!Dead("X3Rodwyn")
!Global("X3Hostile","LOCALS",1)
THEN RESPONSE #100 
JoinParty()
END 

IF 
Global("X3EmiRodwynIsaacDuel","GLOBAL",1)
Dead("X3Isaac")
Dead("X3Rodwyn")
!Global("X3Hostile","LOCALS",1)
THEN RESPONSE #100 
SetGlobal("X3EmiRodwynIsaacDuel","GLOBAL",2)
JoinParty()
END 

IF 
Global("X3EmiRodwynIsaacDuel","GLOBAL",2)
Dead("X3Isaac")
Dead("X3Rodwyn")
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
StartDialogueNoSet(Player1)
END 

//Non Duel Rodwyn Dead Conversation
IF 
Global("X3EmiRodwynIsaacDuel","GLOBAL",0)
Dead("X3Rodwyn")
AreaCheck("%bg1_eet_symbol%3352")
GlobalLT("X3milyTalk","GLOBAL",12)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
Global("X3EmiRodwynDeadConvo","LOCALS",0)
THEN RESPONSE #100 
SetGlobal("X3EmiRodwynDeadConvo","LOCALS",1)
END 

IF 
Global("X3EmiRodwynDeadConvo","LOCALS",1)
GlobalLT("X3milyTalk","GLOBAL",12)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
Global("X3EmiRodwynDeadConvo","LOCALS",1)
THEN RESPONSE #100 
StartDialogueNoSet(Player1)
END 

//Approval Shifts 

IF 
InParty(Myself)
Switch("X3EmilyAppChange","GLOBAL")
THEN RESPONSE #1
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",-10)
DisplayStringNoNameDlg(Player1,@100109)
RESPONSE #2
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",-8)
DisplayStringNoNameDlg(Player1,@100109)
RESPONSE #3
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@100106)
RESPONSE #4
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",-4)
DisplayStringNoNameDlg(Player1,@100106)
RESPONSE #5
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",-2)
DisplayStringNoNameDlg(Player1,@100103)
RESPONSE #6
SetGlobal("X3EmilyAppChange","GLOBAL",0)
RESPONSE #7
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",2)
DisplayStringNoNameDlg(Player1,@100113)
RESPONSE #8
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",4)
DisplayStringNoNameDlg(Player1,@100116)
RESPONSE #9
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",6)
DisplayStringNoNameDlg(Player1,@100116)
RESPONSE #10
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",8)
DisplayStringNoNameDlg(Player1,@100119)
RESPONSE #11
SetGlobal("X3EmilyAppChange","GLOBAL",0)
IncrementGlobal("X3EmilyApp","GLOBAL",10)
DisplayStringNoNameDlg(Player1,@100119)
END 

// If not in party, it should be reset with no approval changes. 
IF 
!InParty(Myself) 
!Global("X3EmilyAppChange","GLOBAL",0)
THEN RESPONSE #100 
SetGlobal("X3EmilyAppChange","GLOBAL",0)
END

//Reset to 120 if past max/reward bonus stats.
IF 
GlobalGT("X3EmilyApp","GLOBAL",120)
InParty(Myself)
Switch("X3EmilyAppMaxBonus","LOCALS")
THEN RESPONSE #0
SetGlobal("X3EmilyApp","GLOBAL",120)
SetGlobal("X3EmilyAppMaxBonus","LOCALS",1)
ChangeStat(Myself,CON,1,ADD)
DisplayStringNoNameDlg(Player1,@10099)
RESPONSE #1 
SetGlobal("X3EmilyApp","GLOBAL",120)
END 

IF 
GlobalLT("X3EmilyApp","GLOBAL",-39)
InParty(Myself)
Switch("X3Leave","LOCALS")
THEN RESPONSE #0
SetGlobal("X3Break","LOCALS",1)
StartDialogueNoSet(Player1)
RESPONSE #2
SetGlobal("X3Break","LOCALS",3)
StartDialogueNoSet(Player1) // Leaves for Good
END

//Custom Approval Shifts - Any Shifts not accompanied directly by dialogue. Make sure they are blocked if they are not in the party.

IF
Dead("MARL")
Global("X3AppMarlDeadCheck","LOCALS",0)
InParty(Myself)
THEN RESPONSE #100
SetGlobal("X3AppMarlDeadCheck","LOCALS",1)
IncrementGlobal("X3EmilyApp","GLOBAL",-4)
DisplayStringNoNameDlg(Player1,@100106)
END 

IF
Dead("MARL")
Global("X3AppMarlDeadCheck","LOCALS",0)
!InParty(Myself)
THEN RESPONSE #100
SetGlobal("X3AppMarlDeadCheck","LOCALS",1)
END 

// Craft Timer 

IF 
PartyRested()
GlobalTimerExpired("X3milyCraftTimer","GLOBAL")
GlobalGT("X3milyAtWork","GLOBAL",0)
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
	StartDialogueNoSet(Player1)
END

// Craft comments 

IF 
Switch("X3ECraft","GLOBAL")
THEN RESPONSE #1 // 1
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2000)
RESPONSE #4 // 3
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2001)
RESPONSE #7 // 5
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2002)
RESPONSE #10 // 7
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2003)
RESPONSE #14 // 10
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2004)
RESPONSE #18 // 13
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2005)
RESPONSE #23 // 17
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2006)
RESPONSE #25 // 18
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2007)
RESPONSE #27 // 19
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2008)
RESPONSE #29 // 20
IncrementGlobal("X3ECraft","GLOBAL",1)
DisplayStringHead(Myself,@2009)
RemoveSpellRES("X3EFA")
RemoveSpellRES("X3EFB")
END 
// Quest Reminder.
IF 
	InParty(Myself)
	OR(2)
	AreaCheck("%bg1_eet_symbol%0800")
	AreaCheck("%bg1_eet_symbol%1300")
	Global("X3milyQuestStart","Global",0)
	GlobalLT("X3DalTalk","GLOBAL",1)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3milyQuestStart","GLOBAL",1)
	StartDialogueNoSet(Player1)
END	 

/* This sets the initial timer. REMEMBER the reseting of the timer must happen in the condition of the Dialogue file, not here. */
IF 
InParty(Myself)
Global("X3milyTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("X3milyTimer","GLOBAL",3200) // TESTING will have this set at a VERY low number. Actual number will be 3200
SetGlobal("X3milyTalk","GLOBAL",1)       // Each block must run only once.
END

// This confusing block controls Emily's timer. We don't need a romance block here, there's no real romance content in the dialog, though I may play with variables in the dialog file itself to remember prior player choices.
IF
InParty(Myself)
RealGlobalTimerExpired("X3milyTimer","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)    
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)                          
!See([ENEMY])
!AreaType(DUNGEON)			  
OR(6)                       // YOU WILL NEED TO ADD +1 TO THIS OR NUMBER FOR EVERY ADDITONAL TALK YOU ADD          
Global("X3milyTalk","GLOBAL",1)
Global("X3milyTalk","GLOBAL",3)
Global("X3milyTalk","GLOBAL",5)
Global("X3milyTalk","GLOBAL",7)
Global("X3milyTalk","GLOBAL",9)
Global("X3milyTalk","GLOBAL",13)
THEN
RESPONSE #100
IncrementGlobal("X3milyTalk","GLOBAL",1)
END

// So, if timer expired, everybody's able to talk and there's no battle, we increase the variable by one. Now our X3milyTalk variable is at 2, or 4, or 6... or 20, or 30.
// EXACTLY like the condition in the beginning of each talk, right?
// Now all that's left is making Branwen talk to you. We could just add StartDialogueNoSet(Player1) into the last block, but the engine is tricky: if you give a command when Branwen's about to talk, she may miss her dialogue!
//So we add another block. 
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(6)                                    // YOU WILL NEED TO ADD +1 TO THIS OR NUMBER FOR EVERY ADDITONAL TALK YOU ADD  
Global("X3milyTalk","GLOBAL",2)
Global("X3milyTalk","GLOBAL",4)
Global("X3milyTalk","GLOBAL",6)
Global("X3milyTalk","GLOBAL",8)
Global("X3milyTalk","GLOBAL",10)
Global("X3milyTalk","GLOBAL",14) // The 14th talk is arrived to as per normal.
THEN
RESPONSE #100
PlaySong(0)
StartDialogueNoSet(Player1)
END
// Sixth Talk Set up, this lasts 45 minutes after the last talk, + 1 hour after the quest is over. As of V1.1 this sets up talks 8/9 as well.
IF
InParty(Myself)
RealGlobalTimerExpired("X3milyTimer","GLOBAL")
Global("X3milyTalk","GLOBAL",11)
Global("X3milySixthTalk","GLOBAL",1)
Global("X3milyHour","GLOBAL",0)
THEN
RESPONSE #100
SetGlobalTimer("X3milyOneHour","GLOBAL",ONE_DAY) // V1.1 Edit: Changing to one day
IncrementGlobal("X3milyHour","GLOBAL",1)
END

IF 
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
GlobalTimerExpired("X3milyOneHour","GLOBAL")
Global("X3milyHour","GLOBAL",1)
OR(2)
Global("X3milyBookTalk","GLOBAL",0) // V1.1 Edit: Changing into a separate talk.
Global("X3milyBookTalk","GLOBAL",3)
THEN 
RESPONSE #100 
IncrementGlobal("X3milyBookTalk","GLOBAL",1)
END 



// New Talk 6 Setup, v1.1
IF 
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
!AreaCheck("%bg1_eet_symbol%3351")
!AreaCheck("%bg1_eet_symbol%3352")
OR(2)
Dead("X3Rodwyn")
GlobalGT("X3EmiRodwynMessageSpawn","GLOBAL",2)
Global("X3milyTalk","GLOBAL",11)
THEN RESPONSE #100 
IncrementGlobal("X3milyTalk","GLOBAL",1)
END 

IF 
Global("X3milyTalk","GLOBAL",12)
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
THEN RESPONSE #100 
PlaySong(0)
//PlaySound("X3ESonD") 
StartDialogueNoSet(Player1)
END 

// New: Beregost Chapter 3 Conversation 
IF 
GlobalGT("Chapter","GLOBAL",2)
GlobalGT("X3milyTalk","GLOBAL",8)
Global("X3milyCraftTalk","GLOBAL",0)
AreaCheck("%bg1_eet_symbol%3300")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
SetGlobal("X3milyCraftTalk","GLOBAL",1)
PlaySong(0)
StartDialogueNoSet(Player1)
END 

// low rep

IF
	InParty(Myself)
    ReputationLT(Player1,8)
	Global("X3milyUnhappy","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(2)
	SetGlobal("X3milyUnhappy","GLOBAL",1)
	StartDialogueNoSet(Player1)
END
// If In Party, Sirene not in party, sees at the temple
IF
	InParty(Myself)
	!InParty("C0Sirene")
    See("C0Sirene")
	AreaCheck("%bg1_eet_symbol%3400")	
	Global("X3milySeeSirene","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3milySeeSirene","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%3900")	
	Global("X3milyUlcaster","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3milyUlcaster","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%2000")	
	Global("X3milyBaldurs","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3milyBaldurs","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%4900")	
	Global("X3milyCarnival","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3milyCarnival","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF 
	Global("X3milyWatchedMomDie","GLOBAL",1)
	!InPartyAllowDead(Myself)
THEN RESPONSE #100 
	SetGlobal("X3milyWatchedMomDie","GLOBAL",0)
END 

IF
	Global("X3milyWatchedMomDie","GLOBAL",1)
	InParty(Myself)
	Dead("X3Dal")	
	Global("X3milyMomDead","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])	
THEN
  RESPONSE #100
	SetGlobal("X3milyMomDead","GLOBAL",1)
END

IF
	Global("X3milyMomDead","GLOBAL",1)
	InParty(Myself)
	See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END

IF
	Global("X3milyMomDead","GLOBAL",2)
	InParty(Myself)
	PartyHasItem("X3EJorn")
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END

IF
	Global("X3milySixthTalk","GLOBAL",1)
	InParty(Myself)
	!Dead("X3Dal")	
	PartyHasItem("X3EJorn")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])	
	Global("X3milyMomAlive","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("X3milyMomAlive","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// SEX CHANGE

IF
    InParty(Myself)
	Gender(Myself,MALE)
	HasItemEquiped("belt05",Myself)
    Global("X3milySexChange","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3milySexChange","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

//Experience
IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,31999)
	XPLT(Myself,32000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,32000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,15999)
	XPLT(Myself,16000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,16000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,7999)
	XPLT(Myself,8000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,8000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,3999)
	XPLT(Myself,4000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,4000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,1999)
	XPLT(Myself,2000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,2000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		SetInterrupt(TRUE)
END